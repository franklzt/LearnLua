#pragma once

// Space Gif by Martijn Steinrucken aka BigWings - 2019
// Email:countfrolic@gmail.com Twitter:@The_ArtOfCode
// License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.
// Original idea from:
// https://boingboing.net/2018/12/20/bend-your-spacetime-continuum.html
//
// To see how this was done, check out this tutorial:
// https://youtu.be/cQXAbndD5CQ
// https://www.shadertoy.com/view/wdlGRM

float4 SpaceGif(in float2 fragCoord,float2 iResolution,float iTime)
{
    float2 uv = (fragCoord.xy-iResolution.xy*.5)/iResolution.y;
	
    float2x2 baseMX = float2x2(.707, -.707, .707, .707);
    uv = mul(baseMX,uv);
    uv *= 15.;
    
    float2 gv = frac(uv)-.5; 
	float2 id = floor(uv);
    
	float m = 0.;
    float t;
    for(float y=-1.; y<=1.; y++) {
    	for(float x=-1.; x<=1.; x++) {
            float2 offs = float2(x, y);
            
            t = -iTime+length(id-offs)*.2;
            float r = lerp(.4, 1.5, sin(t)*.5+.5);
    		float c = smoothstep(r, r*.9, length(gv+offs));
    		m = m*(1.-c) + c*(1.-m);
        }
    }
    return float4(m,m,m,m);
}